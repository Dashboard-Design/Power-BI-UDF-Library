DEFINE
    /// RollingAverage calculates a rolling average over a specified number of periods.
    /// Parameters:
    ///   measure       : SCALAR NUMERIC EXPR - The measure to average (e.g., [Sales]).
    ///   numPeriods    : NUMERIC             - Number of periods to include (positive integer).
    ///   periodUnit    : STRING              - Unit: "DAY", "MONTH", or "YEAR".

    /// Returns: Rolling average of the measure over the last N periods.
    FUNCTION UDF_RollingAverage = 
        (
            _measure      : SCALAR NUMERIC EXPR,
            numPeriods   : NUMERIC,
            periodUnit   : STRING
        ) =>
            --------------------------------------------------------------------
            -- Determine the last date in the current filter context
            --------------------------------------------------------------------
            VAR _LastDate = MAX( 'Date'[Date] )
            VAR _PeriodUnit = UPPER( periodUnit ) 

            --------------------------------------------------------------------
            -- Calculate the average of the measure over each distinct period
            --------------------------------------------------------------------
            VAR _Result =
            SWITCH(
                TRUE(),
                _PeriodUnit = "DAY",
                CALCULATE(
                    AVERAGEX( VALUES( 'Date'[Date] ), _measure ),
                    DATESINPERIOD( 
                        'Date'[Date], 
                        _LastDate, 
                        -numPeriods,          
                        DAY  
                    )
                ),
                _PeriodUnit = "MONTH",
                CALCULATE(
                    AVERAGEX( VALUES( 'Date'[YearMonthNum] ), _measure ),
                    DATESINPERIOD( 
                        'Date'[Date], 
                        _LastDate, 
                        -numPeriods,          
                        MONTH  
                    )
                ),
                _PeriodUnit = "YEAR",
                CALCULATE(
                    AVERAGEX( VALUES( 'Date'[Year] ), _measure ),
                    DATESINPERIOD( 
                        'Date'[Date], 
                        _LastDate, 
                        -numPeriods,          
                        YEAR  
                    )
                )
            )

            RETURN 
                _Result